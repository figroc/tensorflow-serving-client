buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "com.google.protobuf:protobuf-gradle-plugin:0.9.4"
  }
}

//---export-begin---//
ext {
  vers = [tfs: file("VERSION").text.trim(),
          jvm: "8",
          grpc: "1.51.3",
          proto: "3.21.6"]
  dist = [build: "release",
          name: [_: project.name,
                 title: project.name.tokenize("-").collect{it.capitalize()}.join(" "),
                 snake: project.name.replaceAll("-", "_")],
          version: [_: vers.tfs,
                    major: vers.tfs.tokenize('.').first(),
                    grpc: [_: vers.grpc,
                           dotnet: "2.46.5",
                           go: "1.51.0",
                           node: "1.8.21",
                           rust: "0.12.1"],
                    proto: [_: vers.proto,
                            go: "1.5.4",
                            node: "7.1.2",
                            rust: "2.28.0"]],
          license: "Apache-2.0",
          author: [_: "P Chen <figroc@gmail.com>",
                   id: "figroc",
                   name: "P Chen",
                   email: "figroc@gmail.com"],
          url: "https://github.com/figroc/tensorflow-serving-client",
          description: "A prebuilt tensorflow serving client from the tensorflow serving proto files"]
}

version = "${vers.tfs}"

apply plugin: "java-library"

sourceCompatibility = "1.${vers.jvm}"
targetCompatibility = "1.${vers.jvm}"

repositories {
  mavenCentral()
}
//---export-end---//

static def buildEnviron(options, prefix="TFSCLIENT") {
  return options.collectEntries { k, v ->
    def key = k == "_" ? prefix : "${prefix}_${k.toUpperCase()}".toString()
    return (v instanceof Map) ? buildEnviron(v, key) : [(key): v]
  }
}

static def pathOfExecutable(name) {
  def path = "which ${name}".execute()
  path.waitFor()
  return path.in.text.trim()
}

apply plugin: "signing"

apply from: "gradle/grpc.gradle"
apply from: "gradle/cmake.gradle"
apply from: "gradle/wheel.gradle"
apply from: "gradle/rust.gradle"
apply from: "gradle/java.gradle"
apply from: "gradle/node.gradle"
apply from: "gradle/mono.gradle"
apply from: "gradle/golang.gradle"

task buildAll {
  dependsOn "cmake"
  dependsOn "wheel"
  // dependsOn "rust"
  // dependsOn "java"
  // dependsOn "node"
  // dependsOn "mono"
  // dependsOn "golang"
}

apply from: "gradle/ossrh.gradle"

task publishAll {
  dependsOn "publish"
  dependsOn "twine"
  dependsOn "crate"
  dependsOn "npmjs"
  dependsOn "nuget"
}

task __sources__(type: Delete) {
  dependsOn "__cpp__"
  dependsOn "__python__"
  dependsOn "__rust__"
  dependsOn "__java__"
  dependsOn "__node__"
  dependsOn "__golang__"
  dependsOn "__csharp__"
  delete ".github", "build.sh", "update.sh", "Dockerfile", "gradle"
}

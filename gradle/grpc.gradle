apply plugin: "com.google.protobuf"

sourceSets {
  main {
    proto {
      srcDir "proto"
    }
  }
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:${vers.proto}"
  }
  plugins {
    grpc_cpp {
      path = pathOfExecutable("grpc_cpp_plugin")
    }
    grpc_java {
      artifact = "io.grpc:protoc-gen-grpc-java:${vers.grpc}"
    }
    grpc_python {
      path = pathOfExecutable("grpc_python_plugin")
    }
    grpc_csharp {
      path = pathOfExecutable("grpc_csharp_plugin")
    }
  }
  generateProtoTasks {
    all()*.builtins {
      cpp { }
      java { }
      python { }
      csharp {
        option "base_namespace="
      }
    }
    all()*.plugins {
      grpc_cpp {
        outputSubDir = "cpp"
      }
      grpc_java {
        outputSubDir = "java"
      }
      grpc_python {
        outputSubDir = "python"
      }
      grpc_csharp {
        outputSubDir = "csharp"
      }
    }
    all()["generateProto"].finalizedBy "finalizeProtoGrpc"
  }
}

ext {
  protoGenBaseDir = "${buildDir}/generated"
  protoGenBaseRel = file(projectDir).toPath().relativize(file(protoGenBaseDir).toPath())
  protoGenMainDir = "${protobuf.generatedFilesBaseDir}/main"
  protoGenMainRel = file(projectDir).toPath().relativize(file(protoGenMainDir).toPath())
  protoGenMainSub = file(protoGenBaseDir).toPath().relativize(file(protoGenMainDir).toPath())
}

task finalizeProtoGrpc {
  dependsOn "generateProto"
  inputs.dir protoGenMainDir
  doLast {
    delete {
      delete fileTree("${protoGenMainDir}/cpp") {
        include "**/*.grpc.pb.*"
        exclude "**/*_service.grpc.pb.*"
      }
    }
    delete {
      delete fileTree("${protoGenMainDir}/python") {
        include "**/*_pb2_grpc.py"
        exclude "**/*_service_pb2_grpc.py"
      }
    }
  }
}

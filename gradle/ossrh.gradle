apply plugin: "maven-publish"

task sourcesJar(type: Jar) {
  archiveClassifier = "sources"
  from sourceSets.generated.java
}

task javadocJar(type: Jar) {
  archiveClassifier = "javadoc"
  from javadoc
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId project.name
      from components.java
      artifact sourcesJar
      artifact javadocJar
      pom {
        name = dist.name.title
        description = dist.description
        url = dist.url
        scm {
          url = dist.url
          connection = dist.url.replace("https", "scm:git:git").plus(".git")
          developerConnection = dist.url.replace("https", "scm:git:ssh").plus(".git")
        }
        licenses {
          license {
            name = dist.license
            url = "${dist.url}/blob/master/LICENSE"
          }
        }
        developers {
          developer {
            id = dist.author.id
            name = dist.author.name
            email = dist.author.email
          }
        }
      }
    }
  }
  repositories {
    maven {
      url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
      credentials {
        username findProperty("sonatypeUsername")
        password findProperty("sonatypePassword")
      }
    }
  }
}

signing {
  sign publishing.publications.mavenJava
}
